!input cmp
!input typing
@package lang.list
@import lang.typing[True, False]

let List = type[native]

let create = func() {
    intrinsic list_new[list](0)
    new List[native = list]
}
@export create

let len = func(list) {
    intrinsic list_len[len](list.native)
    len
}
@export len

let set_len = func(list, len) {
    intrinsic list_set_len(list.native, len)
}

let cap = func(list) {
    intrinsic list_cap[cap](list.native)
    cap   
}

let grow = func(list) {
    let l = len(list)
    let new_cap = match l == 0
        case True  => 4  # following Rust practice
        case False => l * 2
    intrinsic list_new[new_native](new_cap)
    intrinsic list_copy(list.native, new_native)
    list mut.native = new_native
}

let push = func(list, item) {
    let l = len(list)
    match l == cap(list) case True => grow(list)
    intrinsic list_store(list.native, l, item)
    set_len(list, l + 1)
}
@export push

let get = func(list, index) {
    match index >= len(list) case True => {
        return {}  # TODO throw exception
    }
    intrinsic list_load[item](list.native, index)
    item
}
@export get

let set = func(list, index, item) {
    match index >= len(list) case True => {
        return {}  # TODO throw exception
    }
    intrinsic list_store(list.native, index, item)
}
@export set

# https://doc.rust-lang.org/1.88.0/src/alloc/vec/mod.rs.html#2002
let insert = func(list, index, item) {
    let l = len(list)
    match index > l case True => {
        return {}  # TODO throw exception
    }
    match cap(list) == l case True => grow(list)
    match index < l case True => {
        intrinsic list_copy_within(list.native, index, l - index, index + 1)
    }
    intrinsic list_store(list.native, index, item)
    set_len(list, l + 1)
}
@export insert